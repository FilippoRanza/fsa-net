use lalrpop_util::ParseError;
use crate::syntax_tree;


#[LALR]
grammar;

extern {
    type Error = syntax_tree::TransitionFactoryError; 
}

pub Fsacode : Vec<syntax_tree::Block<'input>> = {
    <block_list: (Block)+> => {
        block_list
    }
}

Block: syntax_tree::Block<'input> = {
    <Network> => syntax_tree::Block::Network(<>),
    <Request> => syntax_tree::Block::Request(<>),
}

Network: syntax_tree::Network<'input> = {
    <begin: @L> "network" <name: Name> "{" <params: NetworkParamList>  "}" <end: @R> => syntax_tree::Network::new(name, params).set_location(begin, end)
}

NetworkParamList : Vec<syntax_tree::NetworkParameterDecl<'input>> = {
    <param_list: (NetworkParameterDecl)+> => param_list
}

NetworkParameterDecl: syntax_tree::NetworkParameterDecl<'input> = {
    <begin: @L> <param: NetworkParameter> <end: @R> => syntax_tree::NetworkParameterDecl::new(param).set_location(begin, end)
}


NetworkParameter : syntax_tree::NetworkParameter<'input> = {
    <AutomataDecl> => syntax_tree::NetworkParameter::Automata(<>),
    <LinkDecl> => syntax_tree::NetworkParameter::Link(<>),
    <EventNameList> => syntax_tree::NetworkParameter::Events(<>),
    <ObserveLabelsList> => syntax_tree::NetworkParameter::ObserveLabels(<>),
    <RelevanceLabelsList> => syntax_tree::NetworkParameter::RelevanceLabels(<>),  
}

EventNameList: Vec<&'input str> = {
    "events" <NameList>
}


ObserveLabelsList: Vec<&'input str> = {
    "obs" <NameList>
}

RelevanceLabelsList: Vec<&'input str> = {
    "rel" <NameList>
}


AutomataDecl: syntax_tree::Automata<'input> = {
    <begin: @L> "automata" <name: Name> "{" <params: AutomataParameterList> "}" <end: @R>  => syntax_tree::Automata::new(name, params).set_location(begin, end)
}

AutomataParameterList : Vec<syntax_tree::AutomataParameterDecl<'input>> = {
    <(AutomataParameterDecl)+>
}

AutomataParameterDecl: syntax_tree::AutomataParameterDecl<'input> = {
    <begin: @L> <param: AutomataParameter> <end: @R> => syntax_tree::AutomataParameterDecl::new(param).set_location(begin, end)
}

AutomataParameter : syntax_tree::AutomataParameter<'input> = {
    <StateDecl> => syntax_tree::AutomataParameter::StateDecl(<>),
    <TransitionDecl> => syntax_tree::AutomataParameter::Transition(<>)
}

StateDecl: syntax_tree::StateDeclaration<'input> = {
    "state" <Name>  => syntax_tree::StateDeclaration::State(<>),
    "begin" <Name>  => syntax_tree::StateDeclaration::Begin(<>)
}

TransitionDecl : syntax_tree::TransitionDeclaration<'input> = {
    <begin: @L> <trans: SimpleTransitionDecl> <end: @R>  => {
        trans.set_location(begin, end)
    },
    <begin: @L> <trans: ComplexTransitionDecl> <end: @R> => {
        trans.set_location(begin, end)
    }
}

SimpleTransitionDecl : syntax_tree::TransitionDeclaration<'input> = {
    "trans" <Name> <Name> <Name>  => syntax_tree::TransitionDeclaration::simple_decl(<>)
}

ComplexTransitionDecl : syntax_tree::TransitionDeclaration<'input> = {
    <begin: @L> "trans" <name: Name> "{" <builder: ComplexTransitionBuilder> "}" <end: @R> =>? {
        let builder = builder.set_location(begin, end);
        match builder.build_transition(name) {
            Ok(trans) => Ok(trans),
            Err(error) => Err(ParseError::User {
                error
            })
        }
    }
}

ComplexTransitionBuilder: syntax_tree::ComplexTransactionFactory<'input> = {
    <list: TransitionParameterList> =>? {
        let res = list.into_iter().try_fold(syntax_tree::ComplexTransactionFactory::new(), |acc, curr| acc.set_parameter(curr));
        match res {
            Ok(builder) => Ok(builder),
            Err(error) => Err(ParseError::User {
                error
            })
        }
    } 
}

TransitionParameterList: Vec<syntax_tree::TransitionKey<'input>> = {
    <list: (TransitionParameter)+> => list
}

TransitionParameter: syntax_tree::TransitionKey<'input> = {
    <begin: @L> "src" <name: Name> <end: @R> => syntax_tree::TransitionKey::new(syntax_tree::TransitionKeys::Src(name) ).set_location(begin, end),
    <begin: @L> "dst" <name: Name> <end: @R> => syntax_tree::TransitionKey::new(syntax_tree::TransitionKeys::Dst(name)).set_location(begin, end),
    <begin: @L> "input" <event: Event> <end: @R> => syntax_tree::TransitionKey::new(syntax_tree::TransitionKeys::Input(event)).set_location(begin, end),
    <begin: @L> "output" <list: EventList> <end: @R> => syntax_tree::TransitionKey::new(syntax_tree::TransitionKeys::Output(list)).set_location(begin, end),
    <begin: @L> "rel" <name: Name> <end: @R> => syntax_tree::TransitionKey::new(syntax_tree::TransitionKeys::Rel(name)).set_location(begin, end),
    <begin: @L> "obs" <name: Name> <end: @R> => syntax_tree::TransitionKey::new(syntax_tree::TransitionKeys::Obs(name)).set_location(begin, end)
}

EventList: Vec<syntax_tree::Event<'input>> = {
    <list: (<Event> ",")*> <event: (Event)> => {
        let mut list = list;
        list.push(event);
        list
    }
}

Event: syntax_tree::Event<'input> = {
    <begin: @L> <event: Name>"(" <link: Name> ")" <end: @R> => syntax_tree::Event::new(event, link).set_location(begin, end)
}

LinkDecl: syntax_tree::Link<'input> = {
    <begin: @L> "link" <name: Name> <src: Name> <dst: Name> <end: @R>  => syntax_tree::Link::new(name, src, dst).set_location(begin, end)
}


Request: syntax_tree::Request<'input> = {
    <begin: @L> "request" <name: Name> "{" <list: RequestParamList>  "}" <end: @R> => syntax_tree::Request::new(name, list).set_location(begin, end)
}

RequestParamList: Vec<syntax_tree::CommandDecl<'input>> = {
    <(<RequestParamDecl> )+> 
}

RequestParamDecl: syntax_tree::CommandDecl<'input> = {
    <begin: @L> <param: RequestParam> <end: @R> => syntax_tree::CommandDecl::new(param).set_location(begin, end)
}



RequestParam: syntax_tree::Command<'input> = {
    "space"  => syntax_tree::Command::Space,
    <begin: @L> "linspace"  <list: NameList> <end: @R> => syntax_tree::Command::Linspace(syntax_tree::LinspaceCommand::new(list).set_location(begin, end)),
    <begin: @L> "diagnosis"  <list: NameList> <end: @R> => syntax_tree::Command::Diagnosis(syntax_tree::DiagnosisCommand::new(list).set_location(begin, end))
}



NameList: Vec<&'input str> = {
    <list: (<Name> ",")*> <name: (Name)> => {
        let mut list = list;
        list.push(name);
        list
    }
}




Name: &'input str = {
    <SimpleName>,
    <SingleQuoteName>,
    <DoubleQuoteName>
}

SimpleName : &'input str = <r"[A-Za-z0-9]+">;
SingleQuoteName:  &'input str = <name: r"'[^']+'"> => syntax_tree::remove_quotes(name);
DoubleQuoteName:  &'input str = <name: r#""[^"]+""#> => syntax_tree::remove_quotes(name);



match {
    r#"\s+"# => {},
    r#"/\*([^*]|\*[^/])*\*/"# => {},
    r#"//[^\n]+"# => {},

    r"\{" => "{",
    r"\}" => "}",
    r"\(" => "(",
    r"\)" => ")",
    r"," => ",",



    r"network" => "network",
    r"events" => "events",
    r"automata" => "automata",
    r"rel" => "rel",
    r"obs" => "obs",
    r"state" => "state",
    r"begin" => "begin",
    r"link" => "link",
    r"request" => "request",
    r"trans" => "trans",
    r"space" => "space",
    r"linspace" => "linspace",
    r"src" => "src",
    r"dst" => "dst",
    r"input" => "input",
    r"output" => "output",
    r"diagnosis" => "diagnosis",



} else {
    r"[A-Za-z0-9]+",
    r"'[^']+'",
    r#""[^"]+""#
}


